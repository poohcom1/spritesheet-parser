plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'edu.sc.seis.launch4j' version '2.5.0'
    id 'maven-publish'
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
}

group 'com.poohcom1'
version '1.0.2'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'junit:junit:4.12'
    implementation 'org.kordamp.ikonli:ikonli-swing:12.2.0'
    implementation 'org.kordamp.ikonli:ikonli-unicons-pack:12.2.0'
    implementation 'org.kordamp.ikonli:ikonli-boxicons-pack:12.2.0'
    implementation 'org.kordamp.ikonli:ikonli-material2-pack:12.2.0'
}

def sourceJarName = "spritesheet-parser.jar"
def fatJarName = "spritesheet-parser-app.jar"


task sourceJar(type: Jar, dependsOn: jar) {
    from(sourceSets.main.output) {
        exclude("com/poohcom1/app/**")
        include("com/poohcom1/spritesheetparser/**")
        exclude("com/poohcom1/spritesheetparser/Test.class")
        processResources.include()
    }
    archiveName(sourceJarName)
}


shadowJar {
    archiveName(fatJarName)
    manifest {
        attributes 'Main-Class': 'com.poohcom1.app.App'
    }

    mergeServiceFiles()
}

createExe {
    headerType="gui"
    mainClassName = 'com.poohcom1.app.App'
    outfile = "Sprite Sheet Parser.exe"
    icon = "${projectDir}/src/main/resources/icon.ico"
    bundledJrePath = "jre"
    bundledJreAsFallback = true
    jarTask = project.tasks.shadowJar
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.poohcom1'
            artifactId = 'spritesheetparser'
            version = '1.0.2'
            artifact(sourceJar)
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/poohcom1/spritesheet-parser"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
